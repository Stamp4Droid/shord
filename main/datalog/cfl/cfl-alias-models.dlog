##################
# CFL Initialization
##################

###################
# CONFIGURATION
###################

# name=cfl-alias-models-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "F.dom"
.include "I.dom"
.include "H.dom"

.bddvarorder V0_V1_M0_C0_M1_C1_M2_C2_I0_F0_H0

##########
# INPUTS #
##########

# points-to facts
pt(c:C,v:V,o:C) input
fpt(o1:C,f:F,o2:C) input

# type filter
HVFilter(v:V,h:H) input

# program statements
Assign(v:V,u:V) input
param(v:V,u:V,i:I) input
return(v:V,u:V,i:I) input
Store(v:V,f:F,u:V) input
Load(v:V,u:V,f:F) input

# basic relations
CC(c:C,d:C) input
CI(c:C,i:I) input
MV(m:M,v:V) input
CM(c:C,m:M) input
CH(o:C,h:H) input

# stub relations
StubParam(m:M,v:V) input
StubReturn(m:M,v:V) input

################
# INTERMEDIATE #
################

# translated points-to facts
Flow(o:C2,c:C0,v:V0) output
FlowField(o1:C,f:F,o2:C) output

# stub entry/exit
Bassign(c:C0,v:V0,m:M0) output
assignE(m:M0,c:C0,v:V0) output

###########
# OUTPUTS #
###########

FlowPre(o:C2,c:C0,m:M0) output
FlowPost(c1:C0,m:M0,c2:C1,v:V0) output
FlowFieldPost(c:C0,m:M0,f:F0,o:C2) output
FlowPrePost(c1:C0,m1:M0,c2:C1,m2:M1) output

FlowPreFull(o:C2,c:C0,m:M0) output
FlowNew(o:C2,c:C0,v:V0) output

#########
# RULES #
#########

# initialization

Flow(o,c,v) :- pt(c,v,o).
FlowField(o1,f,o2) :- fpt(o2,f,o1).

# c is the context of v (*not* m)
Bassign(c,v,m) :- StubParam(m,u), param(u,v,_), MV(mp,v), CM(c,mp).
assignE(m,c,v) :- StubReturn(m,u), return(v,u,_), MV(mp,v), CM(c,mp).

# A_v rules

FlowPre(o,c,m) :- Flow(o,c,v), Bassign(c,v,m).
FlowPre(o,c,m) :- Flow(o,cp,v), Store(u,_,v), FlowPost(c,m,cp,u).
FlowPre(o,c,m) :- FlowField(o,_,op), FlowPre(op,c,m).

# A^v rules

FlowPost(c1,m,c2,v) :- assignE(m,c1,v), c1=c2.
FlowPost(c1,m,c2,v) :- FlowPost(c1,m,c2,u), Assign(v,u).
FlowPost(c1,m,c2,v) :- FlowPost(c1,m,cp,u), param(v,u,i), CC(cp,c2), CI(c2,i).
FlowPost(c1,m,c2,v) :- FlowPost(c1,m,cp,u), return(v,u,i), CC(c2,cp), CI(cp,i).

FlowPost(c1,m,c2,v) :- FlowPost(c1,m,c2,u), Load(v,u,_).
FlowFieldPost(c,m,f,o) :- FlowPost(c,m,cp,v), Store(u,f,v), Flow(o,cp,u).
FlowPost(c1,m,c2,v) :- FlowFieldPost(c1,m,f,o), Flow(o,c2,u), Load(v,u,f).
FlowPost(c1,m,c2,v) :- FlowPre(o,c1,m), Flow(o,c2,u), Load(v,u,_).

# A^w_v rules

FlowPrePost(c1,m1,c2,m2) :- FlowPost(c1,m1,c2,v), Bassign(c2,v,m2).

FlowPrePost(c1,m1,c2,m2) :- FlowFieldPost(c1,m1,_,o), FlowPre(o,c2,m2).
FlowPrePost(c1,m1,c2,m2) :- FlowPre(o,c1,m1), FlowPre(o,c2,m2).
FlowPrePost(c1,m1,c2,m2) :- FlowPost(c1,m1,cp,v), Store(u,_,v), FlowPost(c2,m2,cp,u).

# Full rules

FlowPreFull(o,c,m) :- FlowPre(o,c,m).
FlowPreFull(o,c,m) :- FlowPre(o,cp,mp), FlowPrePost(cp,mp,c,m).
FlowNew(o,c,v) :- FlowPreFull(o,c,m), assignE(m,c,v), HVFilter(v,h), CH(o,h).
