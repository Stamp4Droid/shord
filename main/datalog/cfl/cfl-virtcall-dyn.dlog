#################################
# Virtual call graph resolution #
#################################

# name=cfl-virtcall-dyn-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "I.dom"
.include "S.dom"
.include "T.dom"
.include "Z.dom"

.bddvarorder I0xM0xM1_S0_T0_V0xV1_T1_H0_C0xC1xC2_Z0

##########
# Inputs #
##########

IinvkArg(i:I,n:Z,v:V) input	# invocation i has argument v passed to as the zth parameter
reachableCM(c:C,m:M) input  	# method m is reachable in context c
MI(m:M,i:I) input    		# invocation i is in method m
VirtIM(i:I,s:S) input		# virtual invocation i targets subsignature s
Dispatch(t:T,s:S,m:M) input	# method t.m has subsignature s
HT(h:H,t:T) input     		# heap allocation h has type t
CH(o:C,h:H) input		# abstract object o has heap allocation h on top
ptDyn(c:C,v:V,o:C) input	# v points to abstract object o in context c

################
# Intermediate #
################

reachableCI(c:C,i:I)	# invocation i is reachable in context c
ICM(i:I,o:C,m:M)	# virtual invocation i can call method m when its 0th argument v points to abstract object o

###########
# Outputs #
###########

ptCICMdyn(c:C,i:I,o:C,m:M) output	# virtual invocation i in context c can call method m (with abstract object o as the receiver)
ptIMdyn(i:I,m:M) output            # virtual invocation i can call method m

#########
# Rules #
#########

# intermediate relations

reachableCIV(c,i,v) :- MI(m,i), reachableCM(c,m), IinvkArg(i,0,v).   	      	    # invocation i is reachable, and has receiver v
ICM(i,o,m2) :- VirtIM(i,s), Dispatch(t,s,m2), HT(h,t), CH(o,h).	    		    # virtual invocation i calls method m2 when the receiver points to o

# output relations

ptCICMdyn(c,i,o,m) :- reachableCIV(c,i,v), ptDyn(c,v,o), ICM(i,o,m).
ptIMdyn(i,m) :- ptCICMdyn(_,i,_,m).
