##################
# Static + Dynamic Points-To Edges
##################

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1_H0_I0xF0

###################
# CONFIGURATION
###################

# name=cfl-pt-dyn-dlog

##########
# INPUTS #
##########

# basic relations

Assign(v:V,u:V)     input   # v = u
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

# existing points-to relation

pt(c:C,v:V,o:C) input
fpt(o1:C,f:F,o2:C) input
fptStat(f:F,o:C) input

# dynamic points-to edges

ptdDynOnly(v:V,h:H) input
ptphdDynOnly(v:V,m:M) input

###########
# OUTPUTS #
###########

ptDyn1(c:C,v:V,o:C)

ptDyn(c:C,v:V,o:C) output
fptDyn(o1:C,f:F,o2:C) output
fptStatDyn(f:F,o:C) output

#########
# RULES #
#########

# static seed

ptDyn(c,v,o) :- pt(c,v,o).
fptDyn(o1,f,o2) :- fpt(o1,f,o2).
fptStatDyn(f,o) :- fptStat(f,o).

# dynamic seed

ptDyn(c,v,o) :- ptdDynOnly(v,h), CH(o,h), MV(m,v), CM(c,m).
ptDyn(c,v,o) :- ptphdDynOnly(v,p), CM(o,p), MV(m,v), CM(c,m).

# closure

ptDyn(c,v,o) :- ptDyn(c,u,o), Assign(v,u), typeFilter(v,o).
ptDyn(c,v,o) :- ptDyn1(c,v,o).

ptDyn1(d,u,o) :- ptDyn(c,v,o), param(u,v,i), typeFilter(u,o), CC(c,d), CI(d,i).
ptDyn1(c,u,o) :- ptDyn(d,v,o), return(u,v,i), typeFilter(u,o), CI(d,i), CC(c,d).

fptDyn(o1,f,o2) :- ptDyn(c,v,o2), Store(u,f,v), ptDyn(c,u,o1).
ptDyn1(c,y,o2) :- ptDyn(c,x,o1), Load(y,x,f), fptDyn(o1,f,o2).

fptStatDyn(f,o) :- ptDyn(_,v,o), StoreStat(f,v).
ptDyn1(c,y,o) :- LoadStat(y,f), fptStatDyn(f,o), MV(m,y), CM(c,m).
