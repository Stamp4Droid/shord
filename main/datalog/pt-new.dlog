##################
# Points-to analysis
##################

# name=pt-new-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1_H0xF0_I0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

# intermediate 0-cfa relations
pt0(v:V,o:C) output
fpt0(op:C,f:F,o:C) output

# intermediate k-cfa relations
ptk(c:C,v:V,o:C) output
fptk(op:C,f:F,o:C) output

# query relations
queryObj(o:C) output
queryRef(v:V) output
queryRefCtxt(c:C,v:V) output

##############
# 0-CFA pt
##############

# pt rules
pt0(v,o) :- Alloc(v,h), typeFilter(v,o), CH(o,h).
pt0(u,o) :- pt0(v,o), Assign(u,v), typeFilter(u,o).
pt0(u,o) :- pt0(v,o), param(u,v,_), typeFilter(u,o).
pt0(u,o) :- pt0(v,o), return(u,v,_), typeFilter(u,o).
fpt0(op,f,o) :- pt0(v,o), Store(u,f,v), pt0(u,op).
pt0(x,o) :- fpt0(op,f,o), pt0(w,op), Load(x,w,f), typeFilter(x,o).
pt0(u,o) :- pt0(v,o), StoreStat(f,v), LoadStat(u,f), typeFilter(u,o).

##############
# k-CFA pt
##############

# pt rules
ptk(c,v,o) :- queryObj(o), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).
ptk(c,u,o) :- ptk(c,v,o), Assign(u,v), pt0(u,o).
ptk(d,u,o) :- ptk(c,v,o), param(u,v,i), pt0(u,o), CC(c,d), CI(d,i).
ptk(c,u,o) :- ptk(d,v,o), return(u,v,i), CI(d,i), CC(c,d), pt0(u,o).
fptk(op,f,o) :- ptk(c,v,o), Store(u,f,v), ptk(c,u,op), fpt0(op,f,o).
ptk(d,x,o) :- fptk(op,f,o), ptk(d,w,op), Load(x,w,f), pt0(x,o).
ptk(c,u,o) :- ptk(_,v,o), StoreStat(f,v), LoadStat(u,f), MV(m,u), CM(c,m), pt0(u,o).

# pt ref queries
queryRefCtxt(c,u) :- ptk(c,v,o), Store(u,f,v), pt0(u,op), pt0(w,op), Load(x,w,f), pt0(x,o).
queryRef(w) :- ptk(_,v,o), Store(u,f,v), pt0(u,op), pt0(w,op), Load(x,w,f), pt0(x,o).

# query rules
queryRef(u) :- queryRef(v), Assign(v,u).
queryRefCtxt(d,u) :- queryRef(v), param(v,u,i), CI(d,i).
queryRefCtxt(c,u) :- queryRef(v), return(v,u,i), CI(d,i), CC(c,d).

queryRef(u) :- queryRef(v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).
queryRef(w) :- queryRef(v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).

queryRefCtxt(d,x) :- queryRef(v), Load(v,u,f), ptk(_,u,op), ptk(d,w,op), Store(w,f,x).

ptk(c,v,o) :- queryRef(v), Load(v,u,f), ptk(c,u,op), ptk(d,w,op), Store(w,f,x), ptk(d,x,o), pt0(v,o).
ptk(c,v,o) :- queryRef(v), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).

queryRef(u) :- queryRef(v), LoadStat(v,f), StoreStat(f,u).

ptk(c,v,o) :- queryRef(v), LoadStat(v,f), StoreStat(f,u), ptk(_,u,o), MV(m,v), CM(c,m), pt0(v,o).

# query rules
queryRefCtxt(c,u) :- queryRefCtxt(c,v), Assign(v,u).
queryRefCtxt(d,u) :- queryRefCtxt(c,v), param(v,u,i), CC(c,d), CI(d,i).
queryRefCtxt(d,u) :- queryRefCtxt(c,v), return(v,u,i), CI(d,i), CC(c,d).

queryRefCtxt(c,u) :- queryRefCtxt(c,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).
queryRef(w) :- queryRefCtxt(_,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).

queryRefCtxt(d,x) :- queryRefCtxt(c,v), Load(v,u,f), ptk(c,u,op), ptk(d,w,op), Store(w,f,x).

ptk(c,v,o) :- queryRefCtxt(c,v), Load(v,u,f), ptk(c,u,op), ptk(d,w,op), Store(w,f,x), ptk(d,x,o).
ptk(c,v,o) :- queryRefCtxt(c,v), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).

queryRef(u) :- queryRefCtxt(_,v), LoadStat(v,f), StoreStat(f,u).

ptk(c,v,o) :- queryRefCtxt(c,v), LoadStat(v,f), StoreStat(f,u), ptk(_,u,o), MV(m,v), CM(c,m).

##############
# full pt
##############

queryObj(o) :- Alloc(_,h), CH(o,h).
ptFull(c,v,o) :- ptk(c,v,o).
