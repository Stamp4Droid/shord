RefArg2RefArgTransfer(c:C,v:V,u:V) input
RefArg2RefRetTransfer(c:C,v:V,u:V) input
PrimArg2RefRetTransfer(c:C,v:U,u:V) input
PrimArg2RefRetTransfer(c:C,v:U,u:V) input
RefArg2PrimRetTransfer(c:C,v:V,u:U) input
PrimArg2PrimRetTransfer(c:C,v:U,u:U) input

RefArg2RefArgFlow(c:C,v:V,u:V) input
PrimArg2RefArgFlow(c:C,v:U,u:V) input

SinkFull2ObjBase(c:C,l:CL,o:C) input
SinkFull2PrimBase(c:C,l:CL,v:U) input

## SinkFull2Obj(c:C,l:CL,o:C)
## SinkFull2Prim(c:C,l:CL,v:U)

# SinkFull2Obj :: SinkFull2ObjBase
# SinkFull2Obj :: SinkFull2Obj Obj2ObjField
sink(o2,l) :- sink(o1,l), fpt(o1,_,o2).
# SinkFull2Obj :: Sink2Ref Obj2Ref _RefArg2RefArgFlow _Obj2Ref 
sink(o,l)  :- pt(c,u,o), flowRefRef(u,v), labelRef(c,v,l), sinkCtxtLabel(l).
# SinkFull2Obj :: Sink2Prim _RefArg2PrimArgFlow _Obj2Ref
sink(o,l)  :- pt(c,u,o), flowRefPrim(u,v), labelPrim(c,v,l), sinkCtxtLabel(l).

# SinkFull2Prim :: SinkFull2PrimBase
# SinkFull2Prim :: Sink2Ref _PrimArg2RefArgFlow
sinkPrim(c,u,l) :- flowPrimRef(u,v), labelRef(c,v,l), sinkCtxtLabel(l).
# SinkFull2Prim :: Sink2Prim _PrimArg2PrimArgFlow
sinkPrim(c,u,l) :- flowPrimPrim(u,v), labelPrim(c,v,l), sinkCtxtLabel(l).

## Obj2RefTransfer(c:C,o:C,v:V)
## Obj2PrimTransfer(c:C,o:C,v:U)

# Obj2RefTransfer :: Obj2Ref RefArg2RefArgTransfer
# Obj2RefTransfer :: Obj2Ref RefArg2RefRetTransfer
# Obj2RefTransfer :: _Obj2ObjField Obj2RefTransfer

# Obj2PrimTransfer :: Obj2Ref RefArg2PrimRetTransfer
# Obj2PrimTransfer :: _Obj2ObjField Obj2PrimTransfer

## Src2Sink(src:CL,sink:CL)

# Src2Sink :: Src2Obj _SinkFull2Obj
flow(src,sink) :- sink(o,sink), label(o,src), srcCtxtLabel(src).
# Src2Sink :: Src2Prim _SinkFull2Prim
flow(src,sink) :- sinkPrim(d,u,sink), labelPrim(d,u,src), srcCtxtLabel(src).
# Src2Sink :: Src2PrimField _SinkFull2Obj
flow(src,sink) :- sink(o,sink), labelPrimFld(o,_,src), srcCtxtLabel(src).

## Src2ObjBase(c:C,l:CL,o:C)
## Sink2ObjBase(c:C,l:CL,o:C)

labelIn(o,cl)      :- varInLabel(v,l), MV(m,v), CM(c,m), pt(c,v,o), CCL(c,cl), LCL(l,cl).
labelIn(o2,l)      :- labelIn(o1,l), fpt(o1,_,o2).

## Src2Obj(c:C,l:CL,o:C)
# Src2Obj :: Src2ObjBase
label(o,l)         :- labelIn(o,l).

# Src2ObjX(c:C,l:CL,o:C)
# Src2ObjX :: Src2Obj Obj2RefTransfer _Obj2Ref
labelXferOut(o2,l) :- label(o1,l), transferObjRef(o1,c,v), pt(c,v,o2).
# Src2ObjX :: Src2Prim Prim2RefTransfer _Obj2Ref
labelXferOut(o,l)  :- labelPrim(c,u,l), transferPrimRef(u,v), pt(c,v,o).
# Src2ObjX :: Src2ObjX Obj2ObjField
labelXferOut(o2,l) :- labelXferOut(o1,l), fpt(o1,0,o2).
# Src2ObjX :: Src2PrimField0 Obj2RefTransfer _Obj2Ref
labelXferOut(o2,l) :- labelPrimFld(o1,0,l), transferObjRef(o1,c,v), pt(c,v,o2).

# Src2Obj :: Src2ObjX
label(o,l)         :- labelXferOut(o,l).

# Src2Ref :: Src2Obj _Obj2Ref
labelRef(c,v,l) :- pt(c,v,o), label(o,l).

# primitive-type variables/fields
# label introduction
labelPrim(c,v,cl)     :- varInLabelPrim(v,l), CM(c,m), MU(m,v), LCL(l,cl), CCL(c,cl).
# label propagation
labelPrim(c,v,l)      :- AssignPrim(v,u), labelPrim(c,u,l).
labelPrimFld(o,f,l)   :- labelPrim(c,v,l), StorePrim(u,f,v), pt(c,u,o).
labelPrim(c,y,l)      :- LoadPrim(y,x,f), labelPrimFld(o,f,l), pt(c,x,o).
labelPrimFldStat(f,l) :- StoreStatPrim(f,v), labelPrim(_,v,l).
labelPrim(c,y,l)      :- LoadStatPrim(y,f), labelPrimFldStat(f,l), MU(m,y), CM(c,m).
labelPrim(d,u,l)      :- labelPrim(c,v,l), paramPrim(u,v,i), CC(c,d), CI(d,i).
labelPrim(c,u,l)      :- labelPrim(d,v,l), returnPrim(u,v,i), CI(d,i), CC(c,d).
# passthrough handling
labelPrim(c,v,l)      :- label(o,l), transferObjPrim(o,c,v).
labelPrim(c,v,l)      :- labelPrim(c,u,l), transferPrimPrim(u,v).
# nested tainting
labelPrim(c,y,l)      :- LoadPrim(y,x,_), labelIn(o,l), pt(c,x,o).
# nested passthrough entry
labelPrim(c,v,l)      :- labelPrimFld(o,0,l), transferObjPrim(o,c,v).
# nested passthrough exit
labelPrim(c,y,l)      :- LoadPrim(y,x,0), labelXferOut(o,l), pt(c,x,o).
