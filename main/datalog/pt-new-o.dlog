##################
# Points-to analysis
##################

# name=pt-new-o-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1_H0xF0_I0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

# intermediate 0-cfa relations
pt0o(oa:C,v:V,o:C) output
refpt0o(oa:C,v:V,u:V) output
temppt0o(oa:C,v:V,o:C) output
activeRef0o(oa:C,v:V) output
activeObj0o(oa:C,o:C) output

# intermediate k-cfa relations
ptko(oa:C,c:C,v:V,o:C) output
refptko(oa:C,v:V,c:C,u:V) output
tempptko(oa:C,v:V,o:C) output
activeRefo(oa:C,v:V) output
activeObjo(oa:C,o:C) output

# full pt
ptFullo(c:C,v:V,o:C) output

queryObjo(oa:C,o:C) output
queryRefo(oa:C,v:V) output

##############
# 0-CFA pt
##############

# active rules
activeRef0o(oa,v) :- activeRefo(oa,v).
activeObj0o(oa,o) :- activeObjo(oa,o).

# pt rules
pt0o(oa,v,o) :- activeObj0o(oa,o), Alloc(v,h), typeFilter(v,o), CH(o,h).
pt0o(oa,u,o) :- pt0o(oa,v,o), Assign(u,v), typeFilter(u,o).
pt0o(oa,u,o) :- pt0o(oa,v,o), param(u,v,_), typeFilter(u,o).
pt0o(oa,u,o) :- pt0o(oa,v,o), return(u,v,_), typeFilter(u,o).
pt0o(oa,x,o) :- pt0o(oa,v,o), Store(u,f,v), pt0o(oa,u,op), pt0o(oa,w,op), Load(x,w,f), typeFilter(x,o).
pt0o(oa,u,o) :- pt0o(oa,v,o), StoreStat(f,v), LoadStat(u,f), typeFilter(u,o).

# activeRef rules
activeRef0o(oa,u) :- pt0o(oa,v,o), Store(u,f,v), typeFilter(u,op), typeFilter(w,op), Load(x,w,f), typeFilter(x,o).
activeRef0o(oa,w) :- pt0o(oa,v,o), Store(u,f,v), typeFilter(u,op), typeFilter(w,op), Load(x,w,f), typeFilter(x,o).

# refpt rules
refpt0o(oa,v,u) :- activeRef0o(oa,v), v=u.
refpt0o(oa,v,w) :- refpt0o(oa,v,u), Assign(u,w).
refpt0o(oa,v,w) :- refpt0o(oa,v,u), param(u,w,_).
refpt0o(oa,v,w) :- refpt0o(oa,v,u), return(u,w,_).
refpt0o(oa,v,y) :- refpt0o(oa,v,u), Load(u,w,f), typeFilter(w,o), typeFilter(x,o), Store(x,f,y).
refpt0o(oa,v,w) :- refpt0o(oa,v,u), LoadStat(u,f), StoreStat(f,w).

temppt0o(oa,v,o) :- refpt0o(oa,v,u), Alloc(u,h), CH(o,h).

# activeObj rules
activeObj0o(oa,op) :- pt0o(oa,v,_), Store(u,f,v), temppt0o(oa,u,op), temppt0o(oa,w,op), Load(_,w,f).

##############
# k-CFA pt
##############

# active rules
activeObjo(oa,o) :- queryObjo(oa,o).

# pt rules
ptko(oa,c,v,o) :- activeObjo(oa,o), Alloc(v,h), MV(m,v), pt0o(oa,v,o), CM(c,m), CC(c,o), CH(o,h).
ptko(oa,c,u,o) :- ptko(oa,c,v,o), Assign(u,v), pt0o(oa,u,o).
ptko(oa,d,u,o) :- ptko(oa,c,v,o), param(u,v,i), pt0o(oa,u,o), CC(c,d), CI(d,i).
ptko(oa,c,u,o) :- ptko(oa,d,v,o), return(u,v,i), CI(d,i), CC(c,d), pt0o(oa,u,o).
ptko(oa,d,x,o) :- ptko(oa,c,v,o), Store(u,f,v), ptko(oa,c,u,op), ptko(oa,d,w,op), Load(x,w,f), pt0o(oa,x,o).
ptko(oa,c,u,o) :- ptko(oa,_,v,o), StoreStat(f,v), LoadStat(u,f), MV(m,u), CM(c,m), pt0o(oa,u,o).

# activeRef rules
activeRefo(oa,u) :- ptko(oa,_,v,o), Store(u,f,v), pt0o(oa,u,op), pt0o(oa,w,op), Load(x,w,f), pt0o(oa,x,o), activeRef0o(oa,u).
activeRefo(oa,w) :- ptko(oa,_,v,o), Store(u,f,v), pt0o(oa,u,op), pt0o(oa,w,op), Load(x,w,f), pt0o(oa,x,o), activeRef0o(oa,w).

# refpt rules
refptko(oa,v,c,u) :- activeRefo(oa,v), v=u, MV(m,u), refpt0o(oa,v,u), CM(c,m).
refptko(oa,v,c,w) :- refptko(oa,v,c,u), Assign(u,w), refpt0o(oa,v,w).
refptko(oa,v,c,w) :- refptko(oa,v,d,u), param(u,w,i), refpt0o(oa,v,w), CC(c,d), CI(d,i).
refptko(oa,v,d,w) :- refptko(oa,v,c,u), return(u,w,i), CI(d,i), CC(c,d), refpt0o(oa,v,w).
refptko(oa,v,d,y) :- refptko(oa,v,_,u), Load(u,w,f), typeFilter(w,o), typeFilter(x,o), Store(x,f,y), MV(m,y), CM(d,m), refpt0o(oa,v,y).
refptko(oa,v,d,w) :- refptko(oa,v,_,u), LoadStat(u,f), StoreStat(f,w), MV(m,w), CM(d,m), refpt0o(oa,v,w).

tempptko(oa,v,o) :- refptko(oa,v,_,u), Alloc(u,h), CH(o,h), pt0o(oa,v,o).

# activeObj rules
activeObjo(oa,op) :- ptko(oa,_,v,_), Store(u,f,v), tempptko(oa,u,op), tempptko(oa,w,op), Load(_,w,f), activeObj0o(oa,op).

##############
# full pt
##############

queryObjo(oa,o) :- Alloc(_,h), CH(o,h), o=oa.
ptFullo(c,v,o) :- ptko(oa,c,v,o), o=oa.
