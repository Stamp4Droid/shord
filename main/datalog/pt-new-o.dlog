##################
# Points-to analysis
##################

# name=pt-new-o-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1_H0xF0_I0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

# intermediate 0-cfa relations
pt0(v:V,o:C) output
fpt0(op:C,f:F,o:C) output

# intermediate k-cfa relations
ptko(oa:C,c:C,v:V,o:C) output
fptko(oa:C,op:C,f:F,o:C) output
ptFull(c:C,v:V,o:C) output

# query relations
queryObjo(oa:C,o:C) output
queryRefo(oa:C,v:V) output
queryRefCtxto(oa:C,c:C,v:V) output

##############
# 0-CFA pt
##############

# pt rules
pt0(v,o) :- Alloc(v,h), typeFilter(v,o), CH(o,h).
pt0(u,o) :- pt0(v,o), Assign(u,v), typeFilter(u,o).
pt0(u,o) :- pt0(v,o), param(u,v,_), typeFilter(u,o).
pt0(u,o) :- pt0(v,o), return(u,v,_), typeFilter(u,o).
fpt0(op,f,o) :- pt0(v,o), Store(u,f,v), pt0(u,op).
pt0(x,o) :- fpt0(op,f,o), pt0(w,op), Load(x,w,f), typeFilter(x,o).
pt0(u,o) :- pt0(v,o), StoreStat(f,v), LoadStat(u,f), typeFilter(u,o).

##############
# k-CFA pt
##############

# pt rules
ptko(oa,c,v,o) :- queryObjo(oa,o), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).
ptko(oa,c,u,o) :- ptko(oa,c,v,o), Assign(u,v), pt0(u,o).
ptko(oa,d,u,o) :- ptko(oa,c,v,o), param(u,v,i), pt0(u,o), CC(c,d), CI(d,i).
ptko(oa,c,u,o) :- ptko(oa,d,v,o), return(u,v,i), CI(d,i), CC(c,d), pt0(u,o).
fptko(oa,op,f,o) :- ptko(oa,c,v,o), Store(u,f,v), ptko(oa,c,u,op), fpt0(op,f,o).
ptko(oa,d,x,o) :- fptko(oa,op,f,o), ptko(oa,d,w,op), Load(x,w,f), pt0(x,o).
ptko(oa,c,u,o) :- ptko(oa,_,v,o), StoreStat(f,v), LoadStat(u,f), MV(m,u), CM(c,m), pt0(u,o).

# pt ref queries
queryRefCtxto(oa,c,u) :- ptko(oa,c,v,o), Store(u,f,v), pt0(u,op), pt0(w,op), Load(x,w,f), pt0(x,o).
queryRefo(oa,w) :- ptko(oa,_,v,o), Store(u,f,v), pt0(u,op), pt0(w,op), Load(x,w,f), pt0(x,o).

# query rules
queryRefo(oa,u) :- queryRefo(oa,v), Assign(v,u).
queryRefCtxto(oa,c,u) :- queryRefo(oa,v), param(v,u,i), CC(c,d), CI(d,i).
queryRefCtxto(oa,d,u) :- queryRefo(oa,v), return(v,u,i), CI(d,i).

queryRefo(oa,u) :- queryRefo(oa,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).
queryRefo(oa,w) :- queryRefo(oa,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).

queryRefCtxto(oa,d,x) :- queryRefo(oa,v), Load(v,u,f), ptko(oa,_,u,op), ptko(oa,d,w,op), Store(w,f,x).

ptko(oa,c,v,o) :- queryRefo(oa,v), Load(v,u,f), ptko(oa,c,u,op), ptko(oa,d,w,op), Store(w,f,x), ptko(oa,d,x,o), pt0(v,o).
ptko(oa,c,v,o) :- queryRefo(oa,v), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).

queryRefo(oa,u) :- queryRefo(oa,v), LoadStat(v,f), StoreStat(f,u).

ptko(oa,c,v,o) :- queryRefo(oa,v), LoadStat(v,f), StoreStat(f,u), ptko(oa,_,u,o), MV(m,v), CM(c,m), pt0(v,o).

# query rules
queryRefCtxto(oa,c,u) :- queryRefCtxto(oa,c,v), Assign(v,u).
queryRefCtxto(oa,c,u) :- queryRefCtxto(oa,d,v), param(v,u,i), CC(c,d), CI(d,i).
queryRefCtxto(oa,d,u) :- queryRefCtxto(oa,c,v), return(v,u,i), CI(d,i), CC(c,d).

queryRefCtxto(oa,c,u) :- queryRefCtxto(oa,c,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).
queryRefo(oa,w) :- queryRefCtxto(oa,_,v), Load(v,u,f), pt0(u,op), pt0(w,op), Store(w,f,_).

queryRefCtxto(oa,d,x) :- queryRefCtxto(oa,c,v), Load(v,u,f), ptko(oa,c,u,op), ptko(oa,d,w,op), Store(w,f,x).

ptko(oa,c,v,o) :- queryRefCtxto(oa,c,v), Load(v,u,f), ptko(oa,c,u,op), ptko(oa,d,w,op), Store(w,f,x), ptko(oa,d,x,o).
ptko(oa,c,v,o) :- queryRefCtxto(oa,c,v), Alloc(v,h), MV(m,v), pt0(v,o), CM(c,m), CC(c,o), CH(o,h).

queryRefo(oa,u) :- queryRefCtxto(oa,_,v), LoadStat(v,f), StoreStat(f,u).

ptko(oa,c,v,o) :- queryRefCtxto(oa,c,v), LoadStat(v,f), StoreStat(f,u), ptko(oa,_,u,o), MV(m,v), CM(c,m).

##############
# full pt
##############

queryObjo(oa,o) :- Alloc(_,h), CH(o,h), oa=o.
ptFull(c,v,o) :- ptko(_,c,v,o).
