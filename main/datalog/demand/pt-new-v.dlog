##################
# Points-to analysis
##################

# name=pt-new-v-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1xV2_H0xF0_I0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

# intermediate 0-cfa relations
pt0v(va:V,v:V,o:C) output
fpt0v(va:V,op:C,f:F,o:C) output
refpt0v(va:V,v:V,u:V) output
temppt0v(va:V,v:V,o:C) output
activeRef0v(va:V,v:V) output
activeObj0v(va:V,o:C) output

# intermediate k-cfa relations
ptkv(va:V,c:C,v:V,o:C) output
fptkv(va:V,op:C,f:F,o:C) output
refptkv(va:V,v:V,c:C,u:V) output
tempptkv(va:V,v:V,o:C) output
activeRefv(va:V,v:V) output
activeObjv(va:V,o:C) output

# full pt
ptFullv(c:C,v:V,o:C) output

queryObjv(va:V,o:C) output
queryRefv(va:V,v:V) output

##############
# 0-CFA pt
##############

# active rules
activeRef0v(va,v) :- activeRefv(va,v).
activeObj0v(va,o) :- activeObjv(va,o).

activeObj0v(va,o) :- queryRefv(va,v), temppt0v(va,v,o).

# pt rules
pt0v(va,v,o) :- activeObj0v(va,o), Alloc(v,h), typeFilter(v,o), CH(o,h).
pt0v(va,u,o) :- pt0v(va,v,o), Assign(u,v), typeFilter(u,o).
pt0v(va,u,o) :- pt0v(va,v,o), param(u,v,_), typeFilter(u,o).
pt0v(va,u,o) :- pt0v(va,v,o), return(u,v,_), typeFilter(u,o).
fpt0v(va,op,f,o) :- pt0v(va,v,o), Store(u,f,v), pt0v(va,u,op).
pt0v(va,x,o) :- fpt0v(va,op,f,o), pt0v(va,w,op), Load(x,w,f), typeFilter(x,o).
pt0v(va,u,o) :- pt0v(va,v,o), StoreStat(f,v), LoadStat(u,f), typeFilter(u,o).

# activeRef rules
activeRef0v(va,u) :- pt0v(va,v,o), Store(u,f,v), typeFilter(u,op), typeFilter(w,op), Load(x,w,f), typeFilter(x,o).
activeRef0v(va,w) :- pt0v(va,v,o), Store(u,f,v), typeFilter(u,op), typeFilter(w,op), Load(x,w,f), typeFilter(x,o).

# refpt rules
refpt0v(va,v,u) :- activeRef0v(va,v), v=u.
refpt0v(va,v,w) :- refpt0v(va,v,u), Assign(u,w).
refpt0v(va,v,w) :- refpt0v(va,v,u), param(u,w,_).
refpt0v(va,v,w) :- refpt0v(va,v,u), return(u,w,_).
refpt0v(va,v,y) :- refpt0v(va,v,u), Load(u,w,f), typeFilter(w,o), typeFilter(x,o), Store(x,f,y).
refpt0v(va,v,w) :- refpt0v(va,v,u), LoadStat(u,f), StoreStat(f,w).

temppt0v(va,v,o) :- refpt0v(va,v,u), Alloc(u,h), CH(o,h).

# activeObj rules
activeObj0v(va,op) :- pt0v(va,v,_), Store(u,f,v), temppt0v(va,u,op), temppt0v(va,w,op), Load(_,w,f).

##############
# k-CFA pt
##############

# active rules
#activeObj(va,o) :- queryObjv(va,o).

activeRefv(va,v) :- queryRefv(va,v).
activeObjv(va,o) :- queryRefv(va,v), tempptkv(va,v,o).

# pt rules
ptkv(va,c,v,o) :- activeObjv(va,o), Alloc(v,h), MV(m,v), pt0v(va,v,o), CM(c,m), CC(c,o), CH(o,h).
ptkv(va,c,u,o) :- ptkv(va,c,v,o), Assign(u,v), pt0v(va,u,o).
ptkv(va,d,u,o) :- ptkv(va,c,v,o), param(u,v,i), pt0v(va,u,o), CC(c,d), CI(d,i).
ptkv(va,c,u,o) :- ptkv(va,d,v,o), return(u,v,i), CI(d,i), CC(c,d), pt0v(va,u,o).
fptkv(va,op,f,o) :- ptkv(va,c,v,o), Store(u,f,v), ptkv(va,c,u,op), fpt0v(va,op,f,o).
ptkv(va,d,x,o) :- fptkv(va,op,f,o), ptkv(va,d,w,op), Load(x,w,f), pt0v(va,x,o).
ptkv(va,c,u,o) :- ptkv(va,_,v,o), StoreStat(f,v), LoadStat(u,f), MV(m,u), CM(c,m), pt0v(va,u,o).

# activeRef rules
activeRefv(va,u) :- ptkv(va,_,v,o), Store(u,f,v), pt0v(va,u,op), pt0v(va,w,op), Load(x,w,f), pt0v(va,x,o), activeRef0v(va,u).
activeRefv(va,w) :- ptkv(va,_,v,o), Store(u,f,v), pt0v(va,u,op), pt0v(va,w,op), Load(x,w,f), pt0v(va,x,o), activeRef0v(va,w).

# refpt rules
refptkv(va,v,c,u) :- activeRefv(va,v), v=u, MV(m,u), refpt0v(va,v,u), CM(c,m).
refptkv(va,v,c,w) :- refptkv(va,v,c,u), Assign(u,w), refpt0v(va,v,w).
refptkv(va,v,c,w) :- refptkv(va,v,d,u), param(u,w,i), refpt0v(va,v,w), CC(c,d), CI(d,i).
refptkv(va,v,d,w) :- refptkv(va,v,c,u), return(u,w,i), CI(d,i), CC(c,d), refpt0v(va,v,w).
refptkv(va,v,d,y) :- refptkv(va,v,_,u), Load(u,w,f), typeFilter(w,o), typeFilter(x,o), Store(x,f,y), MV(m,y), CM(d,m), refpt0v(va,v,y).
refptkv(va,v,d,w) :- refptkv(va,v,_,u), LoadStat(u,f), StoreStat(f,w), MV(m,w), CM(d,m), refpt0v(va,v,w).

tempptkv(va,v,o) :- refptkv(va,v,_,u), Alloc(u,h), CH(o,h), pt0v(va,v,o).

# activeObj rules
activeObjv(va,op) :- ptkv(va,_,v,_), Store(u,f,v), tempptkv(va,u,op), tempptkv(va,w,op), Load(_,w,f), activeObj0v(va,op).

##############
# full pt
##############

ptFullv(c,v,o) :- ptkv(va,c,v,o), va=v.
queryRefv(va,v) :- MV(_,v), va=v.
