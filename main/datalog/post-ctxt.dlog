## CONFIGURATION ==============================================================

# Post-processing for context-sensitive analyses
# name=post-ctxt-dlog

.include "Z.dom"
.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "U.dom"
.include "I.dom"
.include "L.dom"
.include "CL.dom"

.bddvarorder Z0_M0_C0_V0_U0_I0_L0xL1_CL0xCL1

## INPUT RELATIONS ============================================================

# Points-to information
CM(c:C,m:M) input

# Method invocations
chaIM(i:I,m:M) input
MmethArg(m:M,z:Z,v:V) input
MmethPrimArg(m:M,z:Z,u:U) input

# Flow information
labelRef(c:C,v:V,cl:CL) input
labelPrim(c:C,u:U,cl:CL) input
flow(c_src:CL,c_sink:CL) input

# Method annotations
SrcLabel(l:L) input
SinkLabel(l:L) input
InLabelRet(l:L,m:M) input
InLabelArg(l:L,m:M,z:Z) input
OutLabelRet(l:L,m:M) input
OutLabelArg(l:L,m:M,z:Z) input
LCL(l:L,cl:CL) input

# Stub facts
Stub(m:M) input

## OUTPUT RELATIONS ===========================================================

# Call graph information
# chaIM(i:I,m:M) will be used directly
out_reachableM(m:M) output

# Taint flow information
out_taintedRefVar(l:L,v:V) output
out_taintedPrimVar(l:L,u:U) output
out_flow(src:L,sink:L) output

# Annotations on reachable methods
out_taintSrc(src:L,m:M) output
out_taintSink(sink:L,m:M) output

# Stub information
out_taintedStub(m:M) output

## RULES ======================================================================

out_reachableM(m) :- CM(_,m).

out_taintedRefVar(l,v)  :- labelRef(_,v,cl), LCL(l,cl).
out_taintedPrimVar(l,u) :- labelPrim(_,u,cl), LCL(l,cl).
out_flow(src,sink)      :- flow(c_src,c_sink), LCL(src,c_src), LCL(sink,c_sink).

out_taintSrc(l,m) :- chaIM(_,m), InLabelRet(l,m), SrcLabel(l).
out_taintSrc(l,m) :- chaIM(_,m), InLabelArg(l,m,_), SrcLabel(l).

out_taintSink(l,m) :- chaIM(_,m), InLabelArg(l,m,_), SinkLabel(l).
out_taintSink(l,m) :- chaIM(_,m), InLabelRet(l,m), SinkLabel(l).
out_taintSink(l,m) :- chaIM(_,m), OutLabelRet(l,m).
out_taintSink(l,m) :- chaIM(_,m), OutLabelArg(l,m,_).

out_taintedStub(m) :- out_taintedRefVar(_,v), MmethArg(m,_,v), Stub(m).
out_taintedStub(m) :- out_taintedPrimVar(_,v), MmethPrimArg(m,_,v), Stub(m).
