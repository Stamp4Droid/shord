##################
# Partial FLows Analysis
##################

###################
# CONFIGURATION
###################

# name=cfl-partial-pt-dlog

.include "Z.dom"
.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder Z0xZ1_M0_C0xC1xC2_V0xV1_H0xF0_I0

###################
# INPUT: POINTER
###################

pt(c:C,v:V,o:C)    input

###################
# INPUT: PARTIAL FLOWS
###################

Assign(v:V,u:V) input              # v = u
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
Store(v:V,f:F,u:V) input           # v.f = u
Load(v:V,u:V,f:F) input            # v = u.f
CI(c:C,i:I) input # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input # there exists i s.t. c++[i] = d

###################
# INPUT: PHANTOM OBJECTS
###################

Varg(v:V,c:C) input
Vret(v:V,c:C) input

###################
# OUTPUT: PARTIAL FLOWS
###################

PreFlowsToA(c:C,o:C,v:V) output
PreFlowsToR(c:C,o:C,v:V) output
PostFlowsToA(c:C,v:V,d:C,u:V) output
PostFlowsToR(c:C,v:V,d:C,u:V) output
MidFlowsToAA(c:C,v:V,d:C,u:V) output
MidFlowsToAR(c:C,v:V,d:C,u:V) output
MidFlowsToRA(c:C,v:V,d:C,u:V) output
MidFlowsToRR(c:C,v:V,d:C,u:V) output

###################
# OUTPUT: FULL FLOWS
###################

RefArg2RefArgTStub(c:C,v:V,u:V,m:M,z1:Z,z2:Z) input
RefArg2RefRetTStub(c:C,v:V,u:V,m:M,z:Z) input
typeFilter(v:V,o:C) input   # type wise, v can point-to o

PreFlowsToFullA(c:C,o:C,v:V) output
PreFlowsToFullR(c:C,o:C,v:V) output
Transfer(c:C,v:V,u:V)

PtFull(c:C,v:V,o:C) output
FptFull(o1:C,f:F,o2:C) output

###################
# RULES: PARTIAL FLOWS
###################

# 1) o -> v_c
PreFlowsToA(c,o,v)  :- pt(c,v,o), Varg(v,c).
# 2) o -> v_c -> u_c -> w_d
PreFlowsToA(d,o,w)  :- pt(c,v,o), Store(u,_,v), PostFlowsToA(d,w,c,u).
PreFlowsToR(d,o,w)  :- pt(c,v,o), Store(u,_,v), PostFlowsToR(d,w,c,u).
# 3) o1 -> v_c -> u_c -> o2 -> w_c
PreFlowsToA(c,o1,w) :- pt(c,v,o1), Store(u,_,v), pt(c,u,o2), PreFlowsToA(c,o2,w).
PreFlowsToR(c,o1,w) :- pt(c,v,o1), Store(u,_,v), pt(c,u,o2), PreFlowsToR(c,o2,w).

# 1) v_c -> v_c
PostFlowsToR(c,v,d,u) :- u=v, d=c, Vret(v,c).
# 2a) v_c -> u_d -> w_d
PostFlowsToA(c,v,d,w) :- PostFlowsToA(c,v,d,u), Assign(w,u).
PostFlowsToR(c,v,d,w) :- PostFlowsToR(c,v,d,u), Assign(w,u).
# 2b) v_c -> u_d -> w_b
PostFlowsToA(c,v,b,w) :- PostFlowsToA(c,v,d,u), param(w,u,i), CC(d,b), CI(b,i).
PostFlowsToA(c,v,b,w) :- PostFlowsToA(c,v,d,u), return(w,u,i), CC(b,d), CI(d,i).
PostFlowsToR(c,v,b,w) :- PostFlowsToR(c,v,d,u), param(w,u,i), CC(d,b), CI(b,i).
PostFlowsToR(c,v,b,w) :- PostFlowsToR(c,v,d,u), return(w,u,i), CC(b,d), CI(d,i).
# 3) v_c -> u_d -> w_d -> o -> x_b -> y_b
PostFlowsToA(c,v,b,y) :- PostFlowsToA(c,v,d,u), Store(w,f,u), pt(d,w,o), pt(b,x,o), Load(y,x,f).
PostFlowsToR(c,v,b,y) :- PostFlowsToR(c,v,d,u), Store(w,f,u), pt(d,w,o), pt(b,x,o), Load(y,x,f).
# 4) v_c -> o -> u_d -> w_d
PostFlowsToA(c,v,d,w) :- PreFlowsToA(c,o,v), pt(d,u,o), Load(w,u,_).
PostFlowsToR(c,v,d,w) :- PreFlowsToR(c,o,v), pt(d,u,o), Load(w,u,_).
# 5) v_c -> u_d -> w_d
PostFlowsToA(c,v,d,w) :- PostFlowsToA(c,v,d,u), Load(w,u,_).
PostFlowsToR(c,v,d,w) :- PostFlowsToR(c,v,d,u), Load(w,u,_).

# 1) v_c -> u_d
MidFlowsToAA(c,v,d,u) :- PostFlowsToA(c,v,d,u), Varg(u,d).
MidFlowsToAR(c,v,d,u) :- PostFlowsToA(c,v,d,u), Vret(u,d).
MidFlowsToRA(c,v,d,u) :- PostFlowsToR(c,v,d,u), Varg(u,d).
MidFlowsToRR(c,v,d,u) :- PostFlowsToR(c,v,d,u), Vret(u,d).
# 2) v_c -> u_d -> w_d -> x_b
MidFlowsToAA(c,v,b,x) :- PostFlowsToA(c,v,d,u), Store(w,_,u), PostFlowsToA(b,x,d,w).
MidFlowsToAR(c,v,b,x) :- PostFlowsToA(c,v,d,u), Store(w,_,u), PostFlowsToR(b,x,d,w).
MidFlowsToRA(c,v,b,x) :- PostFlowsToR(c,v,d,u), Store(w,_,u), PostFlowsToA(b,x,d,w).
MidFlowsToRR(c,v,b,x) :- PostFlowsToR(c,v,d,u), Store(w,_,u), PostFlowsToR(b,x,d,w).
# 3) v_c -> u_d -> w_d -> o -> x_b
MidFlowsToAA(c,v,b,x) :- PostFlowsToA(c,v,d,u), Store(w,_,u), pt(d,w,o), PreFlowsToA(b,o,x).
MidFlowsToAR(c,v,b,x) :- PostFlowsToA(c,v,d,u), Store(w,_,u), pt(d,w,o), PreFlowsToR(b,o,x).
MidFlowsToRA(c,v,b,x) :- PostFlowsToR(c,v,d,u), Store(w,_,u), pt(d,w,o), PreFlowsToA(b,o,x).
MidFlowsToRR(c,v,b,x) :- PostFlowsToR(c,v,d,u), Store(w,_,u), pt(d,w,o), PreFlowsToR(b,o,x).
# 4) v_c -> o -> u_d
MidFlowsToAA(c,v,d,u) :- PreFlowsToA(c,o,v), PreFlowsToA(d,o,u).
MidFlowsToAR(c,v,d,u) :- PreFlowsToA(c,o,v), PreFlowsToR(d,o,u).
MidFlowsToRA(c,v,d,u) :- PreFlowsToR(c,o,v), PreFlowsToA(d,o,u).
MidFlowsToRR(c,v,d,u) :- PreFlowsToR(c,o,v), PreFlowsToR(d,o,u).

###################
# OUTPUT: FULL FLOWS
###################

# helper
Transfer(_,v,u) :- v = u.
Transfer(c,v,u) :- RefArg2RefArgTStub(c,v,u,_,_,_).
Transfer(c,v,u) :- RefArg2RefRetTStub(c,v,u,_,_).
Transfer(c,u,v) :- RefArg2RefRetTStub(c,v,u,_,_).

# full flows

PreFlowsToFullA(c,o,v) :- PreFlowsToA(c,o,v).
PreFlowsToFullR(c,o,v) :- PreFlowsToR(c,o,v).

PreFlowsToFullA(d,o,w) :- PreFlowsToA(c,o,v), Transfer(c,v,u), MidFlowsToAA(c,u,d,w).
PreFlowsToFullA(d,o,w) :- PreFlowsToA(c,o,v), Transfer(c,v,u), MidFlowsToAR(c,u,d,w).
PreFlowsToFullR(d,o,w) :- PreFlowsToR(c,o,v), Transfer(c,v,u), MidFlowsToRA(c,u,d,w).
PreFlowsToFullR(d,o,w) :- PreFlowsToR(c,o,v), Transfer(c,v,u), MidFlowsToRR(c,u,d,w).

PtFull(d,u,o) :- PreFlowsToFullA(c,o,v), PostFlowsToA(c,v,d,u), typeFilter(u,o).
PtFull(d,u,o) :- PreFlowsToFullR(c,o,v), PostFlowsToR(c,v,d,u), typeFilter(u,o).

FptFull(o1,f,o2) :- PtFull(c,v,o2), Store(u,f,v), PtFull(c,u,o1).
