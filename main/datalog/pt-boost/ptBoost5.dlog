##################
# Points-to analysis
##################

# name=pt-boost-5-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"

.bddvarorder M0_C0xC1xC2_V0xV1_H0xF0_I0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

###################
#  FILTER SPECIFIC VARIABLES
###################

ptFilter5(c:C,v:V,o:C) input
fptFilter5(o1:C,f:F,o2:C) input
fptStatFilter5(f:F,o:C) input

ptBoost5(c:C,v:V,o:C) output
fptBoost5(o1:C,f:F,o2:C) output
fptStatBoost5(f:F,o:C) output

###################
# RULES
###################

ptBoost5(c,v,o) :- ptFilter5(c,v,o).
fptBoost5(o1,f,o2) :- fptFilter5(o1,f,o2).
fptStatBoost5(f,o) :- fptStatFilter5(f,o).

ptBoost5(c,v,o) :- Alloc(v,h), MV(m,v), CM(c,m), CC(c,o), CH(o,h).
ptBoost5(c,v,o) :- ptBoost5(c,u,o), Assign(v,u), typeFilter(v,o).
ptBoost5(d,u,o) :- ptBoost5(c,v,o), param(u,v,i), typeFilter(u,o), CC(c,d), CI(d,i).
ptBoost5(c,u,o) :- ptBoost5(d,v,o), return(u,v,i), CI(d,i), CC(c,d), typeFilter(u,o).
fptBoost5(o1,f,o2) :- ptBoost5(c,v,o2), Store(u,f,v), ptBoost5(c,u,o1).
ptBoost5(c,y,o2) :- ptBoost5(c,x,o1), Load(y,x,f), fptBoost5(o1,f,o2).
fptStatBoost5(f,o) :- ptBoost5(_,v,o), StoreStat(f,v).
ptBoost5(c,y,o) :- LoadStat(y,f), fptStatBoost5(f,o), MV(m,y), CM(c,m).
