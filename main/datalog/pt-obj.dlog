##################
# K-obj Points-to analysis
##################

# name=pt-obj-dlog

.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "I.dom"
.include "S.dom"
.include "T.dom"
.include "Z.dom"

.bddvarorder C0xC1_V0xV1_H0_F0_I0_T0xT1_M0_S0_M1_Z0

Assign(v:V,u:V)     input   # v = u
Alloc(v:V,h:H)      input   # v is the lhs in the new stmt h
Load(y:V,x:V,f:F)   input   # y = x.f
Store(u:V,f:F,v:V)  input   # u.f = v
LoadStat(y:V,f:F)   input   # y = f
StoreStat(f:F,v:V)  input   # f = v
param(u:V,v:V,i:I)  input   # u is the formal parameter, v is actual arg, i is invocation stmt
return(u:V,v:V,i:I) input   # u is the lhs at callsite, v is the return var, i is invocation stmt
typeFilter(v:V,o:C) input   # type wise, v can point-to o
IinvkArg(i:I,n:Z,v:V) input

MV(m:M,v:V) input         # v is a variable in method m
CM(c:C,m:M) input         # c is a context of method m
CH(o:C,h:H) input         # contextâ€™ified abstract location o corresponds to new stmt h
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d
HT(h:H,t:T) input         # heap h point to Type t.
MI(m:M,i:I) input         # stmt i belongs to method m.
CI(c:C,i:I) input         # stmt i is on the top of c.

StatIM(i:I,m:M) input     # i statically invokes m. 
SpecIM(i:I,m:M) input     # i special invoke m. 
VirtIM(i:I,m:M) input     # i virtually invokes m. 
classT(t:T) input         # t is concrete or abstract
staticTM(t:T,m:M) input   # m is a static method in t.
staticTF(t:T,f:F) input   # f is a static field in t.
clinitTM(t:T,m:M) input   # m is the class initializer in m.
sub(t1:T0,t2:T1) input    # t1 is a subtype of t2.

SubSig(m:M,s:S) input     # s is subsignature of m.
Dispatch(t:T,s:S,m:M) input     # t.m has subsig s.

###################
#    OUTPUT
###################
CIC(c:C,i:I,d:C) output # d = [i, c]
reachableT(t:T) output
DIC(c:C,i:I,c:C) output
DVDV(c:C0,u:V0,d:C1,v:V1) output
reachableCI(c:C,i:I) output
reachableCM(c:C,m:M) output
CICM(d:C,i:I,c:C,m:M)   output  # i in cotext d calls method m in context c.
pt(c:C,v:V,o:C)    output # (c,v) points-to o
fpt(o1:C,f:F,o2:C) output
fptStat(f:F,o:C) output
ICM(i:I,c:C,m:M) output

###################
#    RULES
###################

reachableCM(0,0).
reachableCM(0,m) :- clinitTM(t,m), reachableT(t).
reachableCM(c,m) :- CICM(_,_,c,m). 
reachableCI(c,i) :- MI(m,i), reachableCM(c,m).

ICM(i,o,m2) :- VirtIM(i,m1), HT(h,t), SubSig(m1,s), Dispatch(t,s,m2), CH(o,h).

DIC(c,i,o) :- IinvkArg(i,0,v), pt(c,v,o).

CIC(c,i,d) :- CC(c,d), CI(d,i).
#kcfaSenStatIM(i,m) :- statIM(i,m), kcfaSenM(m).
#kcfaDIC(c,i,d) :- CIC(c,i,d), MI(m,i), kcfaSenM(m).
CICM(c,i,d,m) :- reachableCI(c,i), StatIM(i,m), CIC(c,i,d).


CICM(c,i,o,m) :- reachableCI(c,i), ICM(i,o,m), DIC(c,i,o).
CICM(c,i,o,m) :- reachableCI(c,i), SpecIM(i,m), DIC(c,i,o).
CICM(c,i,d,m) :- reachableCI(c,i), StatIM(i,m), d=c.

DVDV(d,u,c,v) :- param(u,v,i), MV(m,u), CICM(c,i,d,m).
DVDV(c,u,d,v) :- return(u,v,i), MV(m,v), CICM(c,i,d,m).

pt(c,u,o) :- DVDV(c,u,d,v), pt(d,v,o), typeFilter(u,o).

pt(c,v,o) :- Alloc(v,h), MV(m,v), CM(c,m), CC(c,o), CH(o,h).
pt(c,v,o) :- pt(c,u,o), Assign(v,u), typeFilter(v,o).
fpt(o1,f,o2) :- pt(c,v,o2), Store(u,f,v), pt(c,u,o1).
#u.f=v
#fpt(c1,f,c2) :- pt(c,v,c2), reachableCM(c,m),MV(m,u),Store(u,f,v), pt(c,u,c1).

#RputInstFldInst(c,b,f,r) :- reachableCM(c,m), MputInstFldInst(m,b,f,r).
#CFC(c1,f,c2) :- RputInstFldInst(c,b,f,r), DVC(c,b,c1), DVC(c,r,c2). split

pt(c,y,o2) :- pt(c,x,o1), Load(y,x,f), fpt(o1,f,o2).
fptStat(f,o) :- pt(_,v,o), StoreStat(f,v).
pt(c,y,o) :- LoadStat(y,f), fptStat(f,o), MV(m,y), CM(c,m).

reachableT(t) :- reachableCM(_,m), MV(m,v), Alloc(v,h), HT(h,t).
reachableT(t) :- reachableCM(_,m), staticTM(t,m).
reachableT(t) :- reachableCM(_,m), MV(m,v), StoreStat(f,v), staticTF(t,f).
reachableT(t) :- reachableCM(_,m), MV(m,v), LoadStat(v,f), staticTF(t,f).
reachableT(t) :- classT(t), reachableT(s), sub(s,t).
