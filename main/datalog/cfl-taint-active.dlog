###################
# CONFIGURATION
###################

# name=cfl-taint-active-dlog

.include "Z.dom"
.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "U.dom"
.include "F.dom"
.include "I.dom"
.include "CL.dom"

.bddvarorder Z0xZ1_M0_CL0xCL1_C0xC1xC2_V0xV1_U0xU1_F0_I0

###################
# INPUT: TAINT RELATIONS
###################

pt(c:C,v:V,o:C)    input
fpt(o1:C,f:F,o2:C) input

Store(v:V,f:F,u:V) input           # v.f = u

LoadPrimCtxt(c:C,v:U,u:V,f:F) input
LoadStatPrimCtxt(c:C,v:U,f:F) input
StorePrimCtxt(c:C,v:V,f:F,u:U) input
StoreStatPrimCtxt(c:C,f:F,v:U) input

AssignPrimCtxt(c:C,v:U,u:U) input
AssignPrimCCtxt(c:C,v:U,d:C,u:U) input

# Src label
Src2RefT(c:C,cl:CL,v:V) input
Src2PrimT(c:C,cl:CL,v:U) input

# Sink label
Sink2RefT(c:C,cl:CL,v:V) input
Sink2PrimT(c:C,cl:CL,v:U) input

# Sink full label (out labels)
SinkF2RefF(c:C,cl:CL,v:V) input
SinkF2PrimF(c:C,cl:CL,v:U) input

# Transfer
Ref2RefT(c:C,v:V,u:V) input
Ref2PrimT(c:C,v:V,u:U) input
Prim2RefT(c:C,v:U,u:V) input
Prim2PrimT(c:C,v:U,u:U) input

# Flow (!)
Ref2RefF(c:C,v:V,u:V) input
Ref2PrimF(c:C,v:V,u:U) input
Prim2RefF(c:C,v:U,u:V) input
Prim2PrimF(c:C,v:U,u:U) input

Obj2RefT(c:C,o:C,v:V) input
Obj2PrimT(c:C,o:C,v:U) input

Src2ObjT(cl:CL,o:C) input
Sink2ObjT(cl:CL,o:C) input

# Sink_full-obj flow
SinkF2Obj(cl:CL,o:C) input

# Sink_full-prim flow
SinkF2Prim(c:C,cl:CL,v:U) input

Src2Sink(src:CL,sink:CL) input

# Src-obj flow

Src2Obj(cl:CL,o:C) input
Src2ObjX(cl:CL,o2:C) input

# Sink-obj flow

Sink2Obj(cl:CL,o:C) input
Sink2ObjX(cl:CL,o2:C) input

# Src-prim flow

Src2Prim(c:C,cl:CL,v:U) input
Src2PrimFld(cl:CL,o:C,f:F) input
Src2PrimFldStat(cl:CL,f:F) input

# Sink-prim flow

Sink2Prim(c:C,cl:CL,v:U) input
Sink2PrimFld(cl:CL,o:C,f:F) input
Sink2PrimFldStat(cl:CL,f:F) input

###################
# OUTPUT
###################

ActiveObjTaintSrc(cl:CL,o:C) output
ActivePrimTaintSrc(cl:CL,c:C,v:U) output
ActiveRefTaintSrc(cl:CL,c:C,v:V) output
ActivePrimFldStatTaintSrc(cl:CL,f:F) output

ActiveObjTaintSink(cl:CL,o:C) output
ActivePrimTaintSink(cl:CL,c:C,v:U) output
ActiveRefTaintSink(cl:CL,c:C,v:V) output
ActivePrimFldStatTaintSink(cl:CL,f:F) output

###################
# Src2Sink
###################

#Src2Sink(src,sink) :- Src2Obj(src,o), SinkF2Obj(sink,o). # SinkF2Obj is backwards
ActiveObjTaintSrc(cl,o) :- Src2Obj(cl,o), SinkF2Obj(_,o).
ActiveObjTaintSink(cl,o) :- Src2Obj(_,o), SinkF2Obj(cl,o).
#Src2Sink(src,sink) :- Src2Prim(c,src,v), SinkF2Prim(c,sink,v). # SinkF2Prim is backwards
ActivePrimTaintSrc(cl,c,v) :- Src2Prim(c,cl,v), SinkF2Prim(c,_,v).
ActivePrimTaintSink(cl,c,v) :- Src2Prim(c,_,v), SinkF2Prim(c,cl,v).
#Src2Sink(src,sink) :- Src2PrimFld(src,o,_), SinkF2Obj(sink,o). # SinkF2Obj is backwards
ActiveObjTaintSrc(cl,o) :- Src2PrimFld(cl,o,_), SinkF2Obj(_,o).
ActiveObjTaintSink(cl,o) :- Src2PrimFld(_,o,_), SinkF2Obj(cl,o).

###################
# Src2Obj, Src2ObjX
###################

#Src2ObjX(cl,o2) :- Src2Obj(cl,o1), Obj2RefT(c,o1,v), pt(c,v,o2).
ActiveObjTaintSrc(cl,o1) :- ActiveObjTaintSrc(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Src2Obj(cl,o1).
ActiveRefTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Src2Obj(cl,o1).
#Src2ObjX(cl,o)  :- Src2Prim(c,cl,v), Prim2RefT(c,v,u), pt(c,u,o).
ActiveRefTaintSrc(cl,c,u) :- ActiveObjTaintSrc(cl,o), pt(c,u,o), Prim2RefT(c,v,u), Src2Prim(c,cl,v).
ActivePrimTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o), pt(c,u,o), Prim2RefT(c,v,u), Src2Prim(c,cl,v).
#Src2ObjX(cl,o2) :- Src2PrimFld(cl,o1,0), Obj2RefT(c,o1,v), pt(c,v,o2).
ActiveRefTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Src2PrimFld(cl,o1,0).
ActiveObjTaintSrc(cl,o1) :- ActiveObjTaintSrc(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Src2PrimFld(cl,o1,0).
#Src2ObjX(cl,o2) :- Src2ObjX(cl,o1), fpt(o1,0,o2).
ActiveObjTaintSrc(cl,o1) :- ActiveObjTaintSrc(cl,o2), fpt(o1,0,o2), Src2ObjX(cl,o1).

###################
# Src2Prim, Src2PrimFld
###################

#Src2Prim(c,cl,u) :- Src2Prim(c,cl,v), AssignPrimCtxt(c,u,v). # AssignPrim is flipped
ActivePrimTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,c,u), AssignPrimCtxt(c,u,v), Src2Prim(c,cl,v).
#Src2Prim(d,cl,u) :- Src2Prim(c,cl,v), AssignPrimCCtxt(d,u,c,v). # AssignPrimCtxt is flipped
ActivePrimTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,d,u), AssignPrimCCtxt(d,u,c,v), Src2Prim(c,cl,v).

#Src2Prim(c,cl,v) :- Src2Obj(cl,o), Obj2PrimT(c,o,v).
ActiveObjTaintSrc(cl,o) :- ActivePrimTaintSrc(cl,c,v), Obj2PrimT(c,o,v), Src2Obj(cl,o).
#Src2Prim(c,cl,u) :- Src2Prim(c,cl,v), Prim2PrimT(c,v,u).
ActivePrimTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,c,u), Prim2PrimT(c,v,u), Src2Prim(c,cl,v).

#Src2Prim(c,cl,u) :- Src2ObjT(cl,o), pt(c,v,o), LoadPrimCtxt(c,u,v,_). # pt is backwards, LoadPrimCtxt is flipped
ActiveRefTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,_), pt(c,v,o), Src2ObjT(cl,o).
ActiveObjTaintSrc(cl,o) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,_), pt(c,v,o), Src2ObjT(cl,o).
#Src2Prim(c,cl,u) :- Src2ObjX(cl,o), pt(c,v,o), LoadPrimCtxt(c,u,v,0). # pt is backwards, LoadPrimCtxt is flipped
ActiveRefTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,0), pt(c,v,o), Src2ObjX(cl,o).
ActiveObjTaintSrc(cl,o) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,0), pt(c,v,o), Src2ObjX(cl,o).
#Src2Prim(c,cl,v) :- Src2PrimFld(cl,o,0), Obj2PrimT(c,o,v).
ActiveObjTaintSrc(cl,o) :- ActivePrimTaintSrc(cl,c,v), Obj2PrimT(c,o,v), Src2PrimFld(cl,o,0).

#Src2Prim(c,cl,u) :- Src2PrimFld(cl,o,f), pt(c,v,o), LoadPrimCtxt(c,u,v,f). # pt is backwards, LoadPrim is flipped
ActiveRefTaintSrc(cl,c,v) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,f), pt(c,v,o), Src2PrimFld(cl,o,f).
ActiveObjTaintSrc(cl,o) :- ActivePrimTaintSrc(cl,c,u), LoadPrimCtxt(c,u,v,f), pt(c,v,o), Src2PrimFld(cl,o,f).
#Src2Prim(c,cl,u) :- Src2PrimFldStat(cl,f), LoadStatPrimCtxt(c,u,f). # LoadStatPrimCtxt is flipped
ActivePrimFldStatTaintSrc(cl,f) :- ActivePrimTaintSrc(cl,c,u), LoadStatPrimCtxt(c,u,f), Src2PrimFldStat(cl,f).

#Src2PrimFld(cl,o,f)   :- Src2Prim(c,cl,v), StorePrimCtxt(c,u,f,v), pt(c,u,o). # StorePrimCtxt is flipped
ActiveRefTaintSrc(cl,c,u) :- ActiveObjTaintSrc(cl,o), pt(c,u,o), StorePrimCtxt(c,u,_,v), Src2Prim(c,cl,v).
ActivePrimTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o), pt(c,u,o), StorePrimCtxt(c,u,_,v), Src2Prim(c,cl,v).
#Src2PrimFldStat(cl,f) :- Src2Prim(c,cl,v), StoreStatPrimCtxt(c,f,v). # StoreStatPrimCtxt is flipped
ActivePrimTaintSrc(cl,c,v) :- ActivePrimFldStatTaintSrc(cl,f), StoreStatPrimCtxt(c,f,v), Src2Prim(c,cl,v).

###################
# Sink2Obj, Sink2ObjX
###################

#Sink2ObjX(cl,o2) :- Sink2Obj(cl,o1), Obj2RefT(c,o1,v), pt(c,v,o2).
ActiveObjTaintSink(cl,o1) :- ActiveObjTaintSink(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Sink2Obj(cl,o1).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Sink2Obj(cl,o1).
#Sink2ObjX(cl,o)  :- Sink2Prim(c,cl,v), Prim2RefT(c,v,u), pt(c,u,o).
ActiveRefTaintSink(cl,c,u) :- ActiveObjTaintSink(cl,o), pt(c,u,o), Prim2RefT(c,v,u), Sink2Prim(c,cl,v).
ActivePrimTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,u,o), Prim2RefT(c,v,u), Sink2Prim(c,cl,v).
#Sink2ObjX(cl,o2) :- Sink2PrimFld(cl,o1,0), Obj2RefT(c,o1,v), pt(c,v,o2).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Sink2PrimFld(cl,o1,0).
ActiveObjTaintSink(cl,o1) :- ActiveObjTaintSink(cl,o2), pt(c,v,o2), Obj2RefT(c,o1,v), Sink2PrimFld(cl,o1,0).
#Sink2ObjX(cl,o2) :- Sink2ObjX(cl,o1), fpt(o1,0,o2).
ActiveObjTaintSink(cl,o1) :- ActiveObjTaintSink(cl,o2), fpt(o1,0,o2), Sink2ObjX(cl,o1).

###################
# Sink2Prim, Sink2PrimFld
###################

#Sink2Prim(c,cl,u) :- Sink2Prim(c,cl,v), AssignPrimCtxt(c,u,v). # AssignPrim is flipped
ActivePrimTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), AssignPrimCtxt(c,u,v), Sink2Prim(c,cl,v).
#Sink2Prim(d,cl,u) :- Sink2Prim(c,cl,v), AssignPrimCCtxt(d,u,c,v). # AssignPrimCtxt is flipped
ActivePrimTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,d,u), AssignPrimCCtxt(d,u,c,v), Sink2Prim(c,cl,v).

#Sink2Prim(c,cl,v) :- Sink2Obj(cl,o), Obj2PrimT(c,o,v).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,v), Obj2PrimT(c,o,v), Sink2Obj(cl,o).
#Sink2Prim(c,cl,u) :- Sink2Prim(c,cl,v), Prim2PrimT(c,v,u).
ActivePrimTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), Prim2PrimT(c,v,u), Sink2Prim(c,cl,v).

#Sink2Prim(c,cl,u) :- Sink2ObjT(cl,o), pt(c,v,o), LoadPrimCtxt(c,u,v,_). # pt is backwards, LoadPrimCtxt is flipped
ActiveRefTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,_), pt(c,v,o), Sink2ObjT(cl,o).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,_), pt(c,v,o), Sink2ObjT(cl,o).
#Sink2Prim(c,cl,u) :- Sink2ObjX(cl,o), pt(c,v,o), LoadPrimCtxt(c,u,v,0). # pt is backwards, LoadPrimCtxt is flipped
ActiveRefTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,0), pt(c,v,o), Sink2ObjX(cl,o).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,0), pt(c,v,o), Sink2ObjX(cl,o).
#Sink2Prim(c,cl,v) :- Sink2PrimFld(cl,o,0), Obj2PrimT(c,o,v).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,v), Obj2PrimT(c,o,v), Sink2PrimFld(cl,o,0).

#Sink2Prim(c,cl,u) :- Sink2PrimFld(cl,o,f), pt(c,v,o), LoadPrimCtxt(c,u,v,f). # pt is backwards, LoadPrim is flipped
ActiveRefTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,f), pt(c,v,o), Sink2PrimFld(cl,o,f).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,u), LoadPrimCtxt(c,u,v,f), pt(c,v,o), Sink2PrimFld(cl,o,f).
#Sink2Prim(c,cl,u) :- Sink2PrimFldStat(cl,f), LoadStatPrimCtxt(c,u,f). # LoadStatPrimCtxt is flipped
ActivePrimFldStatTaintSink(cl,f) :- ActivePrimTaintSink(cl,c,u), LoadStatPrimCtxt(c,u,f), Sink2PrimFldStat(cl,f).

#Sink2PrimFld(cl,o,f)   :- Sink2Prim(c,cl,v), StorePrimCtxt(c,u,f,v), pt(c,u,o). # StorePrimCtxt is flipped
ActiveRefTaintSink(cl,c,u) :- ActiveObjTaintSink(cl,o), pt(c,u,o), StorePrimCtxt(c,u,_,v), Sink2Prim(c,cl,v).
ActivePrimTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,u,o), StorePrimCtxt(c,u,_,v), Sink2Prim(c,cl,v).
#Sink2PrimFldStat(cl,f) :- Sink2Prim(c,cl,v), StoreStatPrimCtxt(c,f,v). # StoreStatPrimCtxt is flipped
ActivePrimTaintSink(cl,c,v) :- ActivePrimFldStatTaintSink(cl,f), StoreStatPrimCtxt(c,f,v), Sink2Prim(c,cl,v).

###################
# SinkF
###################

#SinkF2Obj(cl,o)  :- SinkF2RefF(c,cl,v), pt(c,v,o).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,v,o), SinkF2RefF(c,cl,v).
#SinkF2Obj(cl,o2) :- Sink2Obj(cl,o1), pt(c,v,o1), Ref2RefF(c,u,v), pt(c,u,o2). # first pt, Ref2RefF are backwards
ActiveRefTaintSink(cl,c,u) :- ActiveObjTaintSink(cl,o2), pt(c,u,o2), Ref2RefF(c,u,v), pt(c,v,o1), Sink2Obj(cl,o1).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o2), pt(c,u,o2), Ref2RefF(c,u,v), pt(c,v,o1), Sink2Obj(cl,o1).
ActiveObjTaintSink(cl,o1) :- ActiveObjTaintSink(cl,o2), pt(c,u,o2), Ref2RefF(c,u,v), pt(c,v,o1), Sink2Obj(cl,o1).

#SinkF2Obj(cl,o)  :- Sink2Prim(c,cl,v), Ref2PrimF(c,u,v), pt(c,u,o). # Ref2PrimF is backwards
ActiveRefTaintSink(cl,c,u) :- ActiveObjTaintSink(cl,o), pt(c,u,o), Ref2PrimF(c,u,v), Sink2Prim(c,cl,v).
ActivePrimTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,u,o), Ref2PrimF(c,u,v), Sink2Prim(c,cl,v).
#SinkF2Obj(cl,o2) :- SinkF2Obj(cl,o1), fpt(o1,_,o2).
ActiveObjTaintSink(cl,o1) :- ActiveObjTaintSink(cl,o2), fpt(o1,_,o2), SinkF2Obj(cl,o1).

#SinkF2Prim(c,cl,u) :- Sink2Obj(cl,o), pt(c,v,o), Prim2RefF(c,u,v). # pt, Prim2RefF are backwards
ActiveRefTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), Prim2RefF(c,u,v), pt(c,v,o), Sink2Obj(cl,o).
ActiveObjTaintSink(cl,o) :- ActivePrimTaintSink(cl,c,u), Prim2RefF(c,u,v), pt(c,v,o), Sink2Obj(cl,o).
#SinkF2Prim(c,cl,u) :- Sink2Prim(c,cl,v), Prim2PrimF(c,u,v). # Prim2PrimF is backwards
ActivePrimTaintSink(cl,c,v) :- ActivePrimTaintSink(cl,c,u), Prim2PrimF(c,u,v), Sink2Prim(c,cl,v).

###################
# Object annotations
###################

#Obj2RefT(c,o,u)   :- pt(c,v,o), Ref2RefT(c,v,u). # pt is backwards
ActiveRefTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o), pt(c,v,o), Ref2RefT(c,v,u), ActiveRefTaintSrc(cl,c,u).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,v,o), Ref2RefT(c,v,u), ActiveRefTaintSink(cl,c,u).
#Obj2PrimT(c,o,u)  :- pt(c,v,o), Ref2PrimT(c,v,u). # pt is backwards
ActiveRefTaintSrc(cl,c,v) :- ActiveObjTaintSrc(cl,o), pt(c,v,o), Ref2PrimT(c,v,u), ActivePrimTaintSrc(cl,c,u).
ActiveRefTaintSink(cl,c,v) :- ActiveObjTaintSink(cl,o), pt(c,v,o), Ref2PrimT(c,v,u), ActivePrimTaintSink(cl,c,u).
#Obj2RefT(c,o1,v)  :- fpt(o2,0,o1), Obj2RefT(c,o2,v). # fpt is backwards
ActiveObjTaintSrc(cl,o2) :- ActiveObjTaintSrc(cl,o1), fpt(o2,0,o1), Obj2RefT(c,o2,v), ActiveRefTaintSrc(cl,c,v).
ActiveObjTaintSink(cl,o2) :- ActiveObjTaintSink(cl,o1), fpt(o2,0,o1), Obj2RefT(c,o2,v), ActiveRefTaintSink(cl,c,v).
#Obj2PrimT(c,o1,v) :- fpt(o2,0,o1), Obj2PrimT(c,o2,v). # fpt is backwards
ActiveObjTaintSrc(cl,o2) :- ActiveObjTaintSrc(cl,o1), fpt(o2,0,o1), Obj2PrimT(c,o2,v), ActivePrimTaintSrc(cl,c,v).
ActiveObjTaintSink(cl,o2) :- ActiveObjTaintSink(cl,o1), fpt(o2,0,o1), Obj2PrimT(c,o2,v), ActivePrimTaintSink(cl,c,v).

#Src2ObjT(cl,o)  :- Src2RefT(c,cl,v), pt(c,v,o).
ActiveRefTaintSrc(cl,c,v) :- Src2RefT(c,cl,v), pt(c,v,o), ActiveObjTaintSrc(cl,o).
#Src2ObjT(cl,o2) :- Src2ObjT(cl,o1), fpt(o1,_,o2).
ActiveObjTaintSrc(cl,o1) :- Src2ObjT(cl,o1), fpt(o1,_,o2), ActiveObjTaintSrc(cl,o2).

#Sink2ObjT(cl,o)  :- Sink2RefT(c,cl,v), pt(c,v,o).
ActiveRefTaintSink(cl,c,v) :- Sink2RefT(c,cl,v), pt(c,v,o), ActiveObjTaintSink(cl,o).
#Sink2ObjT(cl,o2) :- Sink2ObjT(cl,o1), fpt(o1,_,o2).
ActiveObjTaintSink(cl,o1) :- Sink2ObjT(cl,o1), fpt(o1,_,o2), ActiveObjTaintSink(cl,o2).
