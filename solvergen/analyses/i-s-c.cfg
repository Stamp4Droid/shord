Other :: new | asgnRef | storeRef[*] | loadRef[*] | arrStoreRef | arrLoadRef
       | asgnPrim | storePrim[*] | loadPrim[*] | arrStorePrim | arrLoadPrim
       | prim2Prim | prim2Ref | ref2Prim | ref2Ref

CondFlow :: prim2SinkByPrim | prim2SinkByRef | ref2SinkByPrim | ref2SinkByRef

CallRef[i] :: paramRef[i] | _retRef[i]
CallPrim[i] :: paramPrim[i] | _retPrim[i]
StatMove :: statStoreRef statLoadRef
          | statStorePrim statLoadPrim

LabelIn :: label2Prim | label2Ref
LabelOut :: prim2Sink | ref2Sink

#==============================================================================

# Haven't crossed a conditional flow annotation.
S :: S T1
   | S T3
   | S Other | S _Other
   | -
T1    :: CallRef[i] T2[i]
T2[i] :: S _CallRef[i]
T3    :: CallPrim[i] T4[i]
T4[i] :: S _CallPrim[i]

# Crossed a conditional flow annotation.
# Can only be encountered once, in straight direction.
CS :: CS T1
    | CS T3
    | S CT1
    | S CT3
    | S CondFlow
    | CS Other | CS _Other
CT1    :: CallRef[i] CT2[i]
CT2[i] :: CS _CallRef[i]
CT3    :: CallPrim[i] CT4[i]
CT4[i] :: CS _CallPrim[i]

#==============================================================================

UnbalCall :: CallRef[*] | CallPrim[*]

# Haven't crossed a conditional flow annotation.
P :: srcLabel LabelIn S
   | P _UnbalCall S
   | P StatMove S | P _StatMove S
   | N StatMove S | N _StatMove S

N :: P UnbalCall S
   | N UnbalCall S

# Crossed a single conditional flow annotation.
CP :: srcLabel LabelIn CS
    | P _UnbalCall CS
    | CP _UnbalCall S
    | P StatMove CS | P _StatMove CS
    | N StatMove CS | N _StatMove CS
    | CP StatMove S | CP _StatMove S
    | CN StatMove S | CN _StatMove S

CN :: P UnbalCall CS
    | N UnbalCall CS
    | CP UnbalCall S
    | CN UnbalCall S

#==============================================================================

Src2Sink :: P LabelOut
	  | N LabelOut
          | CP _LabelIn sinkLabel
	  | CN _LabelIn sinkLabel

.paths Src2Sink 1
